plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
archivesBaseName = "TotemChanger-mc1.18.2"
group = "ru.pycat.totemchanger"
evaluationDependsOn(':shared')

repositories {
    maven { name = "Modrinth"
        url = "https://api.modrinth.com/maven" }

    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:1.18.2"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.67.1+1.18.2"

    // ModMenu API
    modApi "com.terraformersmc:modmenu:3.2.5"

    // Cloth Config API
    modApi "me.shedaniel.cloth:cloth-config-fabric:6.4.90"

    // Mods
    modRuntimeOnly "maven.modrinth:smoothboot-fabric:1.18.2-1.7.0"
    modRuntimeOnly "maven.modrinth:lazydfu:0.1.2"
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
